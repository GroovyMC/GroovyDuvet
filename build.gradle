plugins {
    alias libs.plugins.quilt.loom apply false
}

subprojects {
    apply plugin: 'groovy'
    apply plugin: 'maven-publish'
    apply plugin: libs.plugins.quilt.loom.get().pluginId

    //archivesBaseName = "${rootProject.archivesBaseName}-${project.name}"
    version = rootProject.version
    group = rootProject.group

    tasks.withType(JavaCompile).configureEach {
        it.options.encoding = "UTF-8"
        it.options.release = 17
    }

    repositories {
        mavenCentral()
    }

    dependencies {
        minecraft libs.minecraft
        mappings loom.layered() {
            officialMojangMappings()
            parchment("org.parchmentmc.data:parchment-${libs.versions.parchment.minecraft.get()}:${libs.versions.parchment.mappings.get()}@zip")
        }
        modImplementation libs.quilt.loader

        modImplementation libs.qsl

        modImplementation libs.groovy
    }

    java {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
        withSourcesJar()
        withJavadocJar()
    }

    jar {
        from(rootProject.file("LICENSE")) {
            rename { "${it}-${archivesBaseName}" }
        }
        archiveBaseName.set(project.archivesBaseName)
    }

    javadocJar {
        archiveBaseName.set(project.archivesBaseName)
    }

    remapJar {
        archiveBaseName.set(project.archivesBaseName)
        dependsOn(project.tasks.remapSourcesJar)
    }

    remapSourcesJar {
        archiveBaseName.set(project.archivesBaseName)
    }
}