archivesBaseName = "${rootProject.name}-groovy"

dependencies {
    include libs.groovy
    include libs.groovy.contracts
    include libs.groovy.datetime
    include libs.groovy.nio
    include libs.groovy.macro
    include libs.groovy.macro.library
    include libs.groovy.templates
    include libs.groovy.xml
    include libs.groovy.typecheckers
    include libs.groovy.dateutil
    include libs.groovy.ginq
    include libs.groovy.toml
    include libs.groovy.json

    include libs.jackson.core
    include libs.jackson.annotations
    include libs.jackson.databind
    include libs.jackson.dataformat.toml
}

jar {
    manifest.attributes([
            'Specification-Title': 'GroovyDuvet',
            'Specification-Vendor': 'Luke Bemish',
            'Specification-Version': 1,
            'Implementation-Title':"${archivesBaseName}",
            'Implementation-Version': project.version,
            'Implementation-Vendor' : 'Luke Bemish',
            'Implementation-Timestamp': new Date().format(/yyyy-MM-dd'T'HH:mm:ssZ/),
            'GitCommit': getGitCommit(),
            'Automatic-Module-Name': 'io.github.lukebemish.groovyduvet'
    ])
}

import net.fabricmc.loom.task.AbstractRemapJarTask
tasks.withType(AbstractRemapJarTask) {
    targetNamespace = "named"
}

tasks.withType(GenerateModuleMetadata) {
    enabled = false
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId project.group
            artifactId project.archivesBaseName
            version project.version
            from components.java
            artifact(rootProject.changelog.output.get().asFile) {
                it.builtBy rootProject.changelog
                it.extension = 'txt'
                it.classifier = 'changelog'
            }
        }
    }
    repositories {
        maven {
            name = 'ModdingInquisitionMavenRepo'
            url = 'https://maven.moddinginquisition.org/releases'
            credentials {
                username = rootProject.ext.mavenLoginUser
                password = rootProject.ext.mavenLoginPassword
            }
        }
    }
}
