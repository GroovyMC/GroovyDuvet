buildscript {
    dependencies {
        classpath libs.tinyremapper
    }
}

plugins {
    id 'groovy'
    id 'maven-publish'
    alias libs.plugins.quilt.loom
    alias libs.plugins.quilt.licenser
    alias libs.plugins.mdg.plugin
}

license {
    rule rootProject.file('../header.txt')
    exclude '**/*.mcmeta'
}

loom {
    runs.each {
        it.vmArgs '-Dnet.fabricmc.tinyremapper.knownindybsm=org/codehaus/groovy/vmplugin/v8/IndyInterface'
    }
}

version = '1.1.0'
group = 'io.github.lukebemish.groovymc'

modsDotGroovy {
    dslVersion = libs.versions.mdg.dsl.get()
    platform 'quilt'
}

configurations.configureEach {
    resolutionStrategy.eachDependency { DependencyResolveDetails details ->
        if (details.requested.module.with {it?.name == 'tiny-remapper' && it?.group == 'net.fabricmc'}) {
            details.useTarget libs.tinyremapper
        }
    }
}

repositories {
    maven {
        name = "Luke's Maven"
        url = 'https://maven.lukebemish.dev/releases'
    }
    maven {
        name = "TheModdingInquisition"
        url = 'https://maven.moddinginquisition.org/releases'
    }
    maven {
        name = 'ParchmentMC'
        url = 'https://maven.parchmentmc.org'
    }
}

dependencies {
    minecraft libs.minecraft
    mappings loom.layered() {
        officialMojangMappings()
        parchment("org.parchmentmc.data:parchment-${libs.versions.parchment.minecraft.get()}:${libs.versions.parchment.mappings.get()}@zip")
    }
    modImplementation libs.quilt.loader

    modImplementation libs.qsl

    compileOnly libs.groovy
    compileOnly libs.groovy.json

    modImplementation("io.github.lukebemish.groovyduvet:fatjar") { transitive = false }
    modImplementation("io.github.lukebemish.groovyduvet:core") { transitive = false }
    modImplementation("io.github.lukebemish.groovyduvet:wrapper-qsl") { transitive = false }
    modImplementation(libs.cgl)
    runtimeOnly("io.github.lukebemish.groovyduvet:groovy") { transitive = false }
    modRuntimeOnly libs.mappingio
}

jar {
    from(project.file("../LICENSE")) {
        rename { "${it}-${archivesBaseName}" }
    }
}

license {
    header = project.file('../header.txt')
    exclude '**/package-info.java'
}

tasks.withType(JavaCompile).configureEach {
    it.options.encoding = "UTF-8"
    it.options.release = 17
}

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
    withSourcesJar()
}

processResources {
    inputs.property "version", version

    filesMatching('META-INF/groovy/') {
        expand "version": version
    }
}

tasks.withType(GroovyCompile) {
    groovyOptions.optimizationOptions.indy = true
    groovyOptions.optimizationOptions.groovydoc = true
}
