plugins {
    id 'com.modrinth.minotaur' version '2.+'
    id 'com.matthewprenger.cursegradle' version '1.4.0'
}

archivesBaseName = "${rootProject.name}"

configurations {
}

dependencies {
    include project(':core')
    compileOnly project( path: ':core', configuration: 'namedElements' )
    runtimeOnly project(':core')

    include project(':wrapper:minecraft')
    compileOnly project( path: ':wrapper:minecraft', configuration: 'namedElements' )
    runtimeOnly project(':wrapper:minecraft')

    include project(':wrapper:qsl')
    compileOnly project( path: ':wrapper:qsl', configuration: 'namedElements' )
    runtimeOnly project(':wrapper:qsl')
}

jar {
    manifest.attributes([
            'Specification-Title': 'GroovyDuvet',
            'Specification-Vendor': 'Luke Bemish',
            'Specification-Version': 1,
            'Implementation-Title':"${archivesBaseName}",
            'Implementation-Version': project.version,
            'Implementation-Vendor' : 'Luke Bemish',
            'Implementation-Timestamp': new Date().format(/yyyy-MM-dd'T'HH:mm:ssZ/),
            'GitCommit': getGitCommit(),
            'Automatic-Module-Name': 'io.github.lukebemish.groovyduvet'
    ])
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId project.group
            artifactId project.archivesBaseName
            version project.version
            from components.java
            artifact rootProject.tasks.groovydocJar
        }
    }
    repositories {
        maven {
            name = 'ModdingInquisitionMavenRepo'
            url = 'https://maven.moddinginquisition.org/releases'
            credentials {
                username = findProperty('inquisitionMavenUser') ?: ''
                password = findProperty('inquisitionMavenPassword') ?: ''
            }
        }
    }
}

curseforge {
    if (System.getenv('CURSEFORGE_KEY') != null) {
        apiKey = System.getenv('CURSEFORGE_KEY')
        project {
            id = '665821'
            releaseType = "${project.releaseType}"
            addGameVersion "${libs.versions.minecraft.get()}"
            addGameVersion 'Quilt'
            mainArtifact(remapJar) {
                displayName = "GroovyDuvet v$project.version"
            }
            relations {
                requiredDependency 'qsl'
            }
        }
    }
    options {
        forgeGradleIntegration = false
    }
}

if (System.getenv("MODRINTH_KEY") !== null) {
    modrinth {
        token = System.getenv("MODRINTH_KEY")
        projectId = "groovyduvet"
        versionNumber = project.version
        versionType = "${project.releaseType}"
        uploadFile = remapJar
        gameVersions = ["${libs.versions.minecraft.get()}"]
        loaders = ["quilt"]
        dependencies {
            required.project "qsl"
        }
    }
}
